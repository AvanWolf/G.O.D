<?xml version="1.0" ?>
<!DOCTYPE TranscendenceExtension
[
    <!ENTITY modGODCore                 "0xDCBA0500">

    <!ENTITY itGODCoreHook              "0xDCBA0501">
    <!ENTITY stGODCoreStation           "0xDCBA0502">
    <!ENTITY ovGODTimeStop              "0xDCBA0503">

]>

<TranscendenceExtension
  UNID="&modGODCore;"
  name="G.O.D. Core"
  credits="alterecco"
  version="1.0">

<!--
;;;;   License:
;;;;     Public Domain - alterecco@gmail.com
-->

  <Globals>
    (block nil

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;; {{{ CORE VARIABLES
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  ;;; This is a global variable that holds all the modules.
  ;;; We define it like this, since we allow for other
  ;;; files to append their modules as well.
  ;;; Basically it is data to be used for a custompicker list
  (setq god/modules (if (isError god/modules)
    (list)
    god/modules
  ))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;}}}
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;; {{{ MODULE - REFUEL
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (lnkAppend god/modules '(
    (weight       1)
    (title        "Refuel")
    (subtitle     "Refuel your reactor.\nIf it is not fully refueled, contact me!")
    (on-select    (block nil
      (dsf/action 'select "Select" 'S (lambda nil (block nil
        (god/refuel-player)
        (dsf/description "Your Reactor has been refueled")
      )))
    ))
  ))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;}}}
;;;; {{{ MODULE - RESTORE
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (lnkAppend god/modules '(
    (weight       1)
    (title        "Restore Player")
    (subtitle     (cat
      "Restore a target completely.\n"
      "Refuel, fix damage/items, remove negative states, "
      "reset Domina powers and various timers, "
      "fix bad ranks and standings. (some are player only)\n"
      "Credits for this function goes to PM from Transcendence Forums!"
    ))
    (on-select    (block nil
      (dsf/action 'select "Select" 'S (lambda nil (block nil
        (god/restore-target gPlayerShip)
        (dsf/description "Restored your ship")
      )))
    ))
  ))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;}}}
;;;; {{{ MODULE - SPAWN OBJECTS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (lnkAppend god/modules '(
    (weight       0)
    (title        "Spawn Objects")
    (subtitle     "Browse and spawn any object in game. Make them your friends, enemies or wingmen")
    (on-select    (block nil
      (dsf/action 'select "Select" 'S (lambda nil (block nil
        (dsf/spawn-objects)
      )))
    ))
  ))

  (setq dsf/spawn-objects (lambda nil
    (block (spawn)

      (dsf/screen 'spawn-select "Select Spawn" 'custompicker)
      ;; add ships
      (dsf/list-data (fn/list-map (typFind "s") unid
        (list
          (list 'title      (typGetDataField unid 'name))
          (list 'unid       unid)
          (list 'type       'ship)
        )
      ))
      ;; add stations
      (dsf/list-data (fn/list-map (typFind "t -star; -asteroid; -shipwreck; -planet;") unid
        (list
          (list 'title      (typGetDataField unid 'name))
          (list 'unid       unid)
          (list 'type       'station)
        )
      ))
      (dsf/action 'select "Select" 'S (lambda nil
        (block nil
          (dsf/session 'type (dsf/list-entry 'type))
          (dsf/session 'unid (dsf/list-entry 'unid))
          (dsf/show-screen 'spawn-count)
        )
      ))
      (dsf/disable-action 'select (lambda nil (not (dsf/list-entry 'type))))
      (dsf/filter-screen)

      (dsf/screen 'spawn-count "Set Spawn Amount")
      (dsf/pane 'counter)
      (dsf/on-pane-initialize (lambda nil (dsf/counter 1)))
      (dsf/description (lambda nil (block (unid)
        (setq unid (dsf/session 'unid))
        (cat "Select how many \"" (typGetDataField unid 'name) "\" you want to spawn")
      )))
      (dsf/action 'select "Select" 'S (lambda nil
        (block nil
          (dsf/session 'count (fn/constrain (dsf/counter) 1))
          (dsf/show-screen 'spawn-behaviour)
        )
      ))
      (dsf/default-action 'select)

      (dsf/screen 'spawn-behaviour "Set Spawn Behaviour" 'custompicker)
      (dsf/list-data (list (list
        '((title      "Wingman")
          (subtitle   "Add as wingman")
          (orders     'wingman)
          (hide       (eq (dsf/session 'type) 'station))
        )
        '((title      "Enemy")
          (subtitle   "Add as enemy, attacking you")
          (orders     'enemy)
        )
        '((title      "Random Enemy")
          (subtitle   "Add as enemy, attacking random friendly")
          (orders     'randomenemy)
          (hide       true)
        )
        '((title      "Escort")
          (subtitle   "Add as your escort")
          (orders     'escort)
          (hide       (eq (dsf/session 'type) 'station))
        )
        '((title      "Random Patrol")
          (subtitle   "Make the ships patrol around random targets")
          (orders     'patrol)
          (hide       (eq (dsf/session 'type) 'station))
        )
        '((title      "Friendly")
          (subtitle   "Add as friendly")
          (orders     'friendly)
          (hide       (eq (dsf/session 'type) 'ship))
        )
        '((title      "Passive")
          (subtitle   "Add as passive (holding)")
          (orders     'passive)
          (hide       (eq (dsf/session 'type) 'station))
        )
      )))
      (setq spawn (lambda nil
        (block (unid type amount orders pos (objs (list)))
          (setq unid (dsf/session-remove 'unid))
          (setq type (dsf/session-remove 'type))
          (setq amount (dsf/session-remove 'count))
          (setq orders (dsf/list-entry 'orders))

          ;; we create all short of the players pos
          (setq pos (sysVectorPolarOffset gPlayerShip (random 30 150) 5))

          (for i 0 amount
            (switch
              (eq type 'ship)
                (lnkAppend objs (sysCreateShip unid pos &svIndependent;))
              (eq type 'station)
                (lnkAppend objs (sysCreateStation unid pos))
            )
          )

          (if (gr (count objs) 0) (switch
            (eq orders 'wingman)
              (enum objs o
                (block nil
                  (objSetSovereign o &svCommonwealth;)
                  (shpSetPlayerWingman o)
                )
              )
            (eq orders 'enemy)
              (enum objs o
                (block nil
                  (objSetSovereign o &svPirates;)
                  (if (eq type 'ship)
                    (shpOrder o 'attack gPlayerShip)
                  )
                )
              )
            (eq orders 'escort)
              (enum objs o
                (shpOrder o 'escort gPlayerShip)
              )
            (eq order 'patrol)
              (enum objs o
                (shpOrder o 'patrol (random (sysFindObject gPlayerShip "tF")))
              )
            (eq order 'friendly)
              (enum objs o
                (objSetSovereign o &svCommonwealth;)
              )
            (eq order 'passive)
              (enum objs o
                (shpOrder o 'hold)
              )
          ))
        )
      ))
      (dsf/action 'spawn "Spawn And Exit" 'S (lambda nil
        (block nil
          (spawn)
          (dsf/exit)
        )
      ))
      (dsf/action 'spawn-more "Spawn and Add More" 'A (lambda nil
        (block nil
          (spawn)
          (dsf/show-screen 'spawn-select)
        )
      ))
      (dsf/default-action 'spawn)

      (dsf/show-screen 'spawn-select)
    )
  ))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;}}}
;;;; {{{ MODULE - BROWSE ITEMS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (lnkAppend god/modules '(
    (weight       3)
    (title        "Browse Items")
    (subtitle     "Browse and search all items in game. Add some to your cargo hold, or install them directly")
    (on-select    (block nil
      (dsf/action 'select "Select" 'S (lambda nil (block nil
        (dsf/screen 'item-browser "Item Browser")
        (god/item-browser "*")
        (dsf/show-screen)
      )))
    ))
  ))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;}}}
;;;; {{{ MODULE - TRAVEL
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (lnkAppend god/modules '(
    (weight     2)
    (title      "Travel Galaxy")
    (subtitle   "Journey instantly to another system in this galaxy")
    (on-select  (block nil
      (dsf/action 'select "Select" 'S (lambda nil
        (block ((systems (sysGetNodes))
                (current (sysGetNode))
                (data (list)))

          (dsf/screen 'travel-galaxy "Travel Galaxy")

          ;; TODO: build a much more advanced system list (in correct order)
          ;; sort by level
          (setq systems (fn/list-sort systems a b (ls (sysGetLevel a) (sysGetLevel b))))
          ;; filter out endgame (or any others systems with no gates)
          (setq systems (filter systems s (gr (count (sysGetStargates s)) 0)))

          ;; build a custompicker list
          (enum systems s (if (not (eq s current))
            (lnkAppend data (list
              (list 'title (sysGetName s))
              (list 'node s)
            ))
          ))
          (dsf/list-data data)

          (dsf/action 'gate "Select" 'S (lambda nil (block (node gate)
            (setq node (dsf/list-entry 'node))
            (setq gate (item (sysGetStargates node) 0))
            (objGateTo gPlayerShip node gate &efStargateOut;)
            (plyMessage gPlayer (cat "Gating to " (sysGetName node)))
            (dsf/exit)
          )))
          (dsf/disable-action 'gate (lambda nil (not (dsf/list-entry))))

          (dsf/filter-screen)
          (dsf/show-screen)
        )
      ))
    ))
  ))
  (lnkAppend god/modules '(
    (weight     1)
    (title      "Travel Current System")
    (subtitle   "Journey instantly to another place in this system")
    (on-select  (block nil
      (dsf/action 'select "Select" 'S (lambda nil
        (block nil

          (dsf/screen 'travel-system "Travel System")
          (dsf/data-set 'default-filter-term "TAMF")
          (dsf/object-browser)

          (dsf/action 'jump "Select" 'S (lambda nil
            (block (o)
              (setq o (dsf/list-entry 'obj))
              (objJumpTo gPlayerShip (sysVectorRandom (objGetPos o) 8 4 "*"))
              (plyMessage gPlayer (cat "Jumped to " (objGetName o)))
              (dsf/exit)
            )
          ))
          (dsf/disable-action 'jump (lambda nil (not (dsf/list-entry))))

          (dsf/show-screen)
        )
      ))
    ))
  ))

;  ;; system list edge cases:
;  ;;  - destination system has no gates
;  ;;  - current system has only one gate and it leads somewhere
;  ;;    we have already been
;  ;;  - all systems have been accounted for
;  (setq god/gate-list (lambda (systems current gate)
;    (block (all dest)
;      (setq all (sysGetNodes))
;      (setq dest (sysGetStargateDestinationNode current gate))
; 
;      (switch
;        (eq all systems)
;          nil
;        (eq (count (sysGetStargates dest)) 0)
;          nil
;      )
;    )
;  ))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;}}}
;;;; {{{ MODULE - INSTALL DEVICE
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (lnkAppend god/modules '(
    (weight       5)
    (title        "Install Device")
    (subtitle     "Install any device you have in your cargo hold. You can choose between installing it in the regular way, or forcefully")
    (on-select    (block nil
      (dsf/action 'select "Select" 'S (lambda nil
        (god/install-device))
      )
    ))
  ))

  (setq god/install-device (lambda nil
    (block nil
      (dsf/screen 'install-device "Install Device" 'itempicker)
      (dsf/item-filter "dUN")

      (dsf/action 'force "Force Install" 'F (lambda nil (block (result)
        (setq result (god/install-item (dsf/item) nil true))
        (dsf/description (item result 1))
      )))
      (dsf/no-save 'force true)
      (dsf/disable-action 'force (lambda nil (not (dsf/item))))

      (dsf/action 'normal "Normal Install" 'N (lambda nil (block (result)
        (setq result (god/install-item (dsf/item) true true))
        (dsf/description (item result 1))
      )))
      (dsf/no-save 'normal true)
      (dsf/disable-action 'normal (lambda nil (not (dsf/item))))

      (dsf/action 'add "Add More Devices" 'A (lambda nil (block nil
        (dsf/screen 'device-browser "Device Browser")
        (god/item-browser "d")
        (dsf/show-screen)
      )))
      (dsf/show-screen)
    )
  ))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;}}}
;;;; {{{ MODULE - REMOVE DEVICE
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (lnkAppend god/modules '(
    (weight       5)
    (title        "Remove Device")
    (subtitle     "Remove any device you have installed on your ship")
    (on-select    (block nil
      (dsf/action 'select "Select" 'S (lambda nil (block nil
        (setq gPrevScreen (-dsf/get-screen-unid))
        (setq gPrevPane "Default")
        (setq gMargin 0)
        (scrShowScreen gScreen "&dsRemoveDevice;")
      )))
    ))
  ))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;}}}
;;;; {{{ MODULE - REPAIR / REPLACE ARMOR
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (lnkAppend god/modules '(
    (weight       5)
    (title        "Repair or Replace Armor")
    (subtitle     "Fix up your armor, or replace segments")
    (on-select    (block nil
      (dsf/action 'select "Select" 'S (lambda nil (block nil
        (setq gPrevScreen (-dsf/get-screen-unid))
        (setq gPrevPane "Default")
        (setq gTechLevel 15)
        (setq gArmorSegment 0)
        (setq gMargin 0)
        (scrShowScreen gScreen "&dsRepairArmor;")
      )))
    ))
  ))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;}}}
;;;; {{{ MODULE - REPAIR ITEM
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (lnkAppend god/modules '(
    (weight       5)
    (title        "Repair Item")
    (subtitle     "Fix any item that needs fixing")
    (hide         (block ((do-hide true))
      (objEnumItems gPlayerShip itm
        (if (itmIsDamaged itm) (setq do-hide nil))
      )
      do-hide
    ))
    (on-select    (block nil
      (dsf/action 'select "Select" 'S (lambda nil (block nil
        (dsf/screen 'repair-item "Repair Item" 'itempicker)
        (dsf/item-filter "*d~mUD")
        (dsf/action 'repair "Repair" 'R (lambda nil
          (block (itm)
            (shpRepairItem gPlayerShip (dsf/item))
            (dsf/description "Repaired Item")
            ;(dsf/show-screen)
          )
        ))
        (dsf/no-save 'repair true)
        (dsf/disable-action 'repair (lambda nil (not (dsf/item))))
        (dsf/show-screen)
      )))
    ))
  ))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;}}}
;;;; {{{ MODULE - DAMAGE ITEM
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (lnkAppend god/modules '(
    (weight       5)
    (title        "Damage Item")
    (subtitle     "Damage an item... just because")
    (on-select    (block nil
      (dsf/action 'select "Select" 'S (lambda nil (block nil
        (dsf/screen 'repair-item "Repair Item" 'itempicker)
        (dsf/item-filter "*d~mUN")
        (dsf/action 'repair "Damage" 'D (lambda nil
          (block (itm)
            (shpDamageItem gPlayerShip (dsf/item))
            (dsf/description "Damaged Item")
            ;(dsf/show-screen)
          )
        ))
        (dsf/no-save 'repair true)
        (dsf/disable-action 'repair (lambda nil (not (dsf/item))))
        (dsf/show-screen)
      )))
    ))
  ))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;}}}
;;;; {{{ MODULE - MAP SYSTEM
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (lnkAppend god/modules '(
    (weight       5)
    (title        "Map System")
    (subtitle     "Reveal location on map of every station in the current system")
    (on-select    (block nil
      (dsf/action 'select "Select" 'S (lambda nil (block nil
        (enum (sysFindObject gSource "Ts") obj
            (objSetKnown obj)
        )
        (dsf/description "Mapped system")
      )))
    ))
  ))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;}}}
;;;; {{{ MODULE - LOOT ALL
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (lnkAppend god/modules '(
    (weight       6)
    (title        "Loot All")
    (subtitle     "Loot all items in wrecks within a certain radius in ls\n(Note: this does not respect your cargo limit)")
    (on-select    (block nil
      (dsf/action 'select "Select" 'S (lambda nil (block nil
        (if (eq (dsf/pane) 'Counter)
          (block (radius criteria wrecks (cnt 0))
            (setq radius (fn/constrain (dsf/counter) 0 1000))
            (setq criteria (cat "sTN:" radius))
            (setq wrecks (filter (sysFindObject gPlayerShip criteria) obj
              (or (objHasAttribute obj "shipwreck") (eq (objGetVisibleDamage obj) 100) (strFind (objGetName obj) "wreck"))
            ))
            (enum wrecks obj (objEnumItems obj "*U" itm (block nil
              (setq cnt (add cnt 1))
              (objRemoveItem obj itm)
              (objAddItem gPlayerShip itm)
            )))
            (enum wrecks obj (if (objIsShip obj) (objDestroy obj)))
            (dsf/pane 'Default)
            (dsf/description (cat "Looted " (fn/plural cnt "item" "items" true)))
          )
          (block nil
            (dsf/pane 'Counter)
            (dsf/description "Enter the ls you want to set as radius. Upper limit is 1000")
          )
        )
      )))
    ))
  ))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;}}}
;;;; {{{ MODULE - JETTISON ALL
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (lnkAppend god/modules '(
    (weight       6)
    (title        "Jettison All")
    (subtitle     "Jettison all items in your hold")
    (on-select    (block nil
      (dsf/action 'select "Select" 'S (lambda nil (block (items)
        (if (setq items (objGetItems gPlayerShip "*U"))
          (block (dest)
            (setq dest (sysCreateStation &stCargoCrate; (sysVectorPolarOffset gPlayerShip 0 2)))
            (enum items itm (block nil
              (objRemoveItem gPlayerShip itm)
              (objAddItem dest itm)
            ))
            (dsf/description "Jettisoned all items")
          )
          (dsf/description "You have no items onboard to jettison")
        )
      )))
    ))
  ))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;}}}
;;;; {{{ MODULE - BANKING DEPOSIT CREDITS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (lnkAppend god/modules '(
    (weight       6)
    (title        "Banking - Deposit Credits")
    (subtitle     "Add credits to your account")
    (on-select    (block nil
      (dsf/action 'select "Select" 'S (lambda nil (block nil
        (if (eq (dsf/pane) 'Counter)
          (block (credits)
            (plyCredit gPlayer (setq credits (fn/constrain (dsf/counter) 0 10000000)))
            (dsf/pane 'Default)
            (dsf/description (cat "Added " credits " credits to your account"))
          )
          (block nil
            (dsf/pane 'Counter)
            (dsf/description "Select the amount of credits you want (max 10000000)")
          )
        )
      )))
    ))
  ))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;}}}
;;;; {{{ MODULE - BANKING WITHDRAW CREDITS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (lnkAppend god/modules '(
    (weight       6)
    (title        "Banking - Withdraw Credits")
    (subtitle     "Remove credits from your account")
    (on-select    (block nil
      (dsf/action 'select "Select" 'S (lambda nil (block (mx)
        (setq mx (plyGetCredits gPlayer))
        (if (eq (dsf/pane) 'Counter)
          (block (credits)
            (plyCharge gPlayer (setq credits (fn/constrain (dsf/counter) 0 mx)))
            (dsf/pane 'Default)
            (dsf/description (cat "Withdrew " credits " credits from your account"))
          )
          (block nil
            (dsf/pane 'Counter)
            (dsf/description (cat "Select the amount of credits you want to remove (max " mx ")"))
          )
        )
      )))
    ))
  ))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;}}}
;;;; {{{ MODULE - BANKING DEPOSIT RINS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (lnkAppend god/modules '(
    (weight       6)
    (title        "Banking - Deposit Rins")
    (subtitle     "Add rins to your account")
    (on-select    (block nil
      (dsf/action 'select "Select" 'S (lambda nil (block (rins)
        (setq rins (or (objGetData gPlayerShip 'rins) 0))
        (if (eq (dsf/pane) 'Counter)
          (block (credit)
            (setq rins (add rins (setq credit (fn/constrain (dsf/counter) 0 10000000))))
            (objSetData gPlayerShip 'rins rins)
            (dsf/pane 'Default)
            (dsf/description (cat "Added " credit " rins to your account. You now have " rins " rins"))
          )
          (block nil
            (dsf/pane 'Counter)
            (dsf/description (cat "You have " rins " rins. How many do you want to add (max 10000000)"))
          )
        )
      )))
    ))
  ))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;}}}
;;;; {{{ MODULE - BANKING WITHDRAW RINS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (lnkAppend god/modules '(
    (weight       6)
    (title        "Banking - Withdraw Rins")
    (subtitle     "Remove rins from your account")
    (on-select    (block nil
      (dsf/action 'select "Select" 'S (lambda nil (block (rins)
        (setq rins (objGetData gPlayerShip 'rins))
        (if (eq (dsf/pane) 'Counter)
          (block (credit)
            (setq rins (subtract rins (setq credit (fn/constrain (dsf/counter) 0 rins))))
            (objSetData gPlayerShip 'rins rins)
            (dsf/pane 'Default)
            (dsf/description (cat "Withdraw " credit " rins from your account. You now have " rins " rins"))
          )
          (block nil
            (dsf/pane 'Counter)
            (dsf/description (cat "Select the amount of rins you want to remove (max " rins ")"))
          )
        )
      )))
    ))
  ))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;}}}
;;;; {{{ MODULE - TIMESTOP
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (lnkAppend god/modules '(
    (weight     9)
    (title      "Time Stop")
    (subtitle   "Stop time and start it again. Great for taking a look around")
    (on-select  (block (state)
      (setq state (objGetData gPlayerShip 'god/keeptimestop))
      (dsf/action 'toggle-time
        (if state "Start Time" "Stop Time") 'S
        (lambda nil (if state
          (block nil
            (objSetData gPlayerShip 'god/keeptimestop nil)
            (dsf/description "Started time")
          )
          (block nil
            (objSetData gPlayerShip 'god/keeptimestop true)
            (dsf/description "Stopped time")
          )
        ))
      )
    ))
  ))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;}}}
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;; {{{ CORE SCREEN
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (setq god/core-screen (lambda nil (block (modules)
    (dsf/screen 'god-core "G.O.D" 'custompicker)
    (dsf/no-back true)

    ;; sort list by weight
    (setq modules (fn/list-sort god/modules a b (ls (fn/hash-get a 'weight) (fn/hash-get b 'weight))))
    (dsf/list-data modules)

    ;; timestop handlers
    (dsf/on-init (lambda nil
      (if (not (objGetData gPlayerShip 'god/timeoverlay))
        (objSetData gPlayerShip 'god/timeoverlay (objAddOverlay gPlayerShip &ovGODTimeStop;))
      )
    ))
    (dsf/on-global-exit (lambda nil (block (oID keepStopped)
      (setq oID (objGetData gPlayerShip 'god/timeoverlay))
      (setq keepStopped (objGetData gPlayerShip 'god/keeptimestop))
      (if (and (not keepStopped) oID) (block nil
        (objRemoveOverlay gPlayerShip oID)
        (objSetData gPlayerShip 'god/timeoverlay nil)
      ))
    )))

    (dsf/filter-screen)
    (dsf/show-screen)
  )))


;    (setq god/core-screen (lambda nil (block nil
;
;      (dsf/screen 'god-core "G.O.D")
;      (dsf/filter-screen)
;      (dsf/list-data (god/format-modules))
;
;      (dsf/action 'select "Select" 'S (lambda nil
;        (dsf/eval (dsf/list-entry 'action))
;      ))
;      (dsf/save-action 'select true)
;
;      ;; timestop handlers
;      (dsf/on-init (lambda nil
;        (if (not (objGetData gPlayerShip 'god/timeoverlay))
;          (objSetData gPlayerShip 'god/timeoverlay (objAddOverlay gPlayerShip &ovGODTimeStop;))
;        )
;      ))
;      (dsf/on-global-exit (lambda nil (block (oID keepStopped)
;        (setq oID (objGetData gPlayerShip 'god/timeoverlay))
;        (setq keepStopped (objGetData gPlayerShip 'god/keeptimestop))
;        (if (and (not keepStopped) oID) (block nil
;          (objRemoveOverlay gPlayerShip oID)
;          (objSetData gPlayerShip 'god/timeoverlay nil)
;        ))
;      )))
;
;      (dsf/exit-action)
;      (dsf/show-screen)
;    )))
;
;    ;; gather all the G.O.D modules into one custompicker list
;    (setq god/format-modules (lambda nil (block (modules (list))
;      (if (not (setq modules god/formatted-modules)) (block nil
;        ;; sort list once by weight, then by title
;        (setq modules (dsf/list-sort god/modules a b (ls (dsf/hash-get a 'weight) (dsf/hash-get b 'weight))))
;        ;(setq modules (dsf/list-sort modules a b (ls (dsf/hash-get a 'title) (dsf/hash-get b 'title))))
;        (setq god/formatted-modules modules)
;      ))
;      modules
;    )))
;
;    ;;; This global variable is used to store the formatted list of modules
;    ;;; so that we don't have to recreate it on every run.
;    ;;; This allows us to make the building of the list more expensive
;    ;;; We don't save this on the ship so we can catch changes to the
;    ;;; modules made between sessions
;    (setq god/formatted-modules nil)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;}}}
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

    )
  </Globals>

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;; {{{ CORE ITEMS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  <ItemType UNID="&itGODCoreHook;"
    name=           "G.O.D."
    virtual=        "true"
    usekey=         "G"
    >
    <Image imageID="&rsItems1;" imageX="0" imageY="192" imageWidth="96" imageHeight="96"/>
    <Invoke>
      (god/core-screen)
    </Invoke>
  </ItemType>

  <StationType UNID="&stGODCoreStation;"
    virtual="true"
    >
    <Events>
      <OnGlobalPlayerEnteredSystem>
        (block (itm)
          (if (not (objHasItem gPlayerShip (setq itm (itmCreate &itGODCoreHook; 1))))
            (objAddItem gPlayerShip itm)
          )
        )
      </OnGlobalPlayerEnteredSystem>
    </Events>
  </StationType>

  <OverlayType UNID="&ovGODTimeStop;">
    <Events>
      <OnUpdate>
        (sysStopTime 30 gPlayerShip)
      </OnUpdate>
    </Events>
  </OverlayType>

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;}}}
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  <Globals>
    (block Nil

    (setq god/item-browser (lambda (criteria)
      (block nil
        (dsf/item-browser (fn/list-map (itmGetTypes criteria) itm (itmCreate itm 1)))

        (dsf/action 'add-to-cargo "Add to Cargo Hold" 'A (lambda nil
          (block nil
            (if (eq (dsf/pane) "Counter")
              (block (cnt itm inventory)
                ;; add item
                (setq cnt (dsf/counter))
                (setq itm (itmCreate (itmGetUNID (dsf/item)) cnt))
                (objAddItem gPlayerShip itm)
                (dsf/pane "Default")
                (dsf/action-label 'add-to-cargo "Add to Cargo Hold")
                (dsf/action-key 'add-to-cargo 'A)
                (dsf/description "Your request has been fulfilled")
              )
              (block nil
                (dsf/pane "Counter")
                (dsf/counter 1)
                (dsf/action-label 'add-to-cargo "Accept Transaction")
                (dsf/action-key 'add-to-cargo 'A)
                (dsf/description (cat "Select the amount of \"" (itmGetName (dsf/item) 3) "\" you want to add to your cargo hold"))
              )
            )
          )
        ))
        (dsf/no-save 'add-to-cargo true)
        (dsf/disable-action 'add-to-cargo (lambda nil (not (dsf/item))))

        (dsf/action 'install "Install Item" 'N (lambda nil
          (block (itm result)
            (setq itm (dsf/item))
            (setq result (god/install-item itm nil nil))
            (dsf/description (item result 1))
          )
        ))
        (dsf/no-save 'install true)
        (dsf/disable-action 'install (lambda nil
          (or (not (dsf/item)) (not (itmMatches (dsf/item) "d")))
        ))

        (dsf/action 'identify-single "Identify Current Item" 'C (lambda nil
          (block (itm)
            (setq itm (dsf/item))
            (itmSetKnown itm)
            (dsf/description "Identified item")
          )
        ))
        (dsf/no-save 'identify-single true)
        (dsf/disable-action 'identify-single (lambda nil (block (itm)
          (or (not (setq itm (dsf/item))) (itmIsKnown itm))
        )))

        (dsf/action 'identify-all "Identify All Items" 'I (lambda nil
          (block nil
            (enum (itmGetTypes criteria) itm (block nil
              (itmSetKnown itm)
            ))
            (dsf/description "Identified all items")
          )
        ))
        (dsf/no-save 'identify-all true)
        (dsf/disable-action 'identify-all (lambda nil (not (dsf/item))))
      )
    ))

    (setq god/refuel-player (lambda nil
      ;; pump it full of Longzhu's
      (shpRefuelFromItem gPlayerShip (itmCreate &itLongzhuSphere; 2000))
    ))

    (setq god/restore-target (lambda (target)
      (block nil

        (if (objIsShip target) (block nil
          ;; fix states
          (objFixParalysis target)
          (shpFixBlindness target)
          (shpDecontaminate target)

          ;; repair shields
          (shpRechargeShield target 99999999)

          ; repair all items/devices
          (objEnumItems target "*~a D" itm (shpRepairItem target itm))

          ;; refuel
          (if (eq gPlayerShip target) (block nil
            (god/refuel-player)

            ;; fix domina and ranks
            (typSetGlobalData &stDomina; "lastInvokeTime1" 0)
            (typSetGlobalData &stDomina; "lastInvokeTime2" 0)
            (typSetGlobalData &stDomina; "lastInvokeTime3" 0)
            (typSetGlobalData &stDomina; "lastInvokeTime4" 0)
            (typSetGlobalData &stDomina; "lastInvokeTime5" 0)
            (typSetGlobalData &stDomina; "lastInvokeTime6" 0)

          ))
        ))

        ;; repair armor
        (objEnumItems target "a" armor
          (objRepairArmor target armor)
        )

      )
    ))

    ;; Install an item, using the normal rules, or forcefully.
    ;; We return a tuple with an code and a message
    ;;
    ;; - normal
    ;;   Use the normal rules for installing
    ;; - cargo
    ;;   We have the item in our cargo already
    ;; - target
    ;;   Which ship to install on
    (setq god/install-item (lambda (itm normal inCargo target)
      (block (canInstall result code)
        ;; if we don't have a target, it is the player
        (if (not target) (setq target gPlayerShip))

        (setq canInstall (shpCanInstallDevice target itm))

        (switch
          (and (itmHasModifier itm "Military") (not (objGetItems gPlayerShip "*+MilitaryID")))
            (setq result '(1 "You must have a military ID to install this item."))
          (eq canInstall 2)
            (setq result '(2 "Your ship cannot support any more devices."))
          (and (itmHasModifier itm "Illegal") (not (objGetItems gPlayerShip "*+BlackMarketID")))
            (setq result '(3 "You must have a black market ID to install this item."))
          (eq canInstall 4)
            (setq result '(4 "Your ship already has a shield generator installed."))
          (eq canInstall 5)
            (setq result '(5 "Your ship already has a propulsion system installed."))
          (eq canInstall 6)
            (setq result '(6 "Your ship already has a missile launcher installed."))
          (eq canInstall 7)
            (setq result '(7 "Your ship's reactor is not powerful enough for this device."))
          (eq canInstall 8)
            (setq result '(8 "Your ship already has a cargo expansion system installed."))
          (eq canInstall 9)
            (setq result '(9 "Your ship already has a reactor installed."))
          ;; XXX: this one can perhaps be 'normal'
          (eq canInstall 10)
            (setq result '(10 "The cargo expansion system is too large to be installed in your ship."))
          ;; XXX: this one can perhaps be 'normal'
          (eq canInstall 11)
            (setq result '(11 "The power output of the item is too high for your ship."))
          (eq canInstall 12)
            (setq result '(12 "Your ship cannot support any more weapons."))
          (eq canInstall 13)
            (setq result '(13 "Your ship cannot support any more non-weapon devices."))
          (not (eq canInstall 0))
            (setq result '(14 "This item can not be installed."))
          (setq result '(true))
        )
        ;; if result is True, then we install right away
        (setq code (item result 0))
        (if (or code (find '(4 5 6 8 9) code) (and (not normal) (find '(1 3) code)))
          (block (name)
            (setq name (itmGetName itm 1))
            (switch
              (eq code 4)
                (setq result (list true (cat "Replaced shield generator with " name)))
              (eq code 5)
                (setq result (list true (cat "Replaced propulsion system with " name)))
              (eq code 6)
                (block Nil
                  (shpRemoveDevice target (item (objGetItems target "Il") 0))
                  (setq result (list true (cat "Replaced missile launcher with " name)))
                )
              (eq code 8)
                (setq result (list true (cat "Replaced current cargo expansion with " name)))
              (eq code 9)
                (setq result (list true (cat "Replaced reactor with " name)))
              (setq result (list true (cat "Installed " name)))
            )
            (if (not inCargo) (objAddItem target itm))
            (shpInstallDevice target itm)
          )
        )
        result
      )
    ))

    )
  </Globals>
</TranscendenceExtension>
<!--
vim:ts=2:sw=2:sts=2:ft=tscript:
-->
